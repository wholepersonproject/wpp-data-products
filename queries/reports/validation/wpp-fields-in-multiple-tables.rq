#+ summary: WPP time scales in tables

PREFIX ccf: <http://purl.org/ccf/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wpp: <https://purl.wholepersonphysiome.org/schema/wpp#>
PREFIX CL: <http://purl.obolibrary.org/obo/CL_>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>

PREFIX HRA: <https://purl.humanatlas.io/collection/hra>
PREFIX WPP: <https://purl.wholepersonphysiome.org/collection/wpp>

SELECT ?field ?value (GROUP_CONCAT(DISTINCT ?table;SEPARATOR='|') as ?wpp_tables) (COUNT(DISTINCT ?table) as ?table_count)
FROM WPP:
WHERE {
  
  [
    a wpp:Record ;
    wpp:record_source ?source ;
    ?field_pred ?value ;
  ] .

  FILTER(isLITERAL(?value) && ?field_pred != wpp:record_number)
  BIND(REPLACE(STR(?source), 'https://purl.wholepersonphysiome.org/wpp/', '') as ?table)
  BIND(REPLACE(REPLACE(STR(?field_pred), STR(wpp:), ''), '_field', '') as ?field)
}
GROUP BY ?field ?value
ORDER BY ?field DESC(?table_count)
