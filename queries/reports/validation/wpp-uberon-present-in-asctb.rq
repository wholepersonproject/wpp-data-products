#+ summary: get WPP collection components and download URLs for them

PREFIX ccf: <http://purl.org/ccf/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wpp: <https://purl.wholepersonphysiome.org/schema/wpp#>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>

PREFIX HRA: <https://purl.humanatlas.io/collection/hra>
PREFIX WPP: <https://purl.wholepersonphysiome.org/collection/wpp>

SELECT ?id ?label (GROUP_CONCAT(DISTINCT ?table;SEPARATOR='|') as ?wpp_tables)
WHERE {
  GRAPH HRA: {
    ?iri ccf:ccf_asctb_type "AS"^^xsd:string .
    ?iri rdfs:label ?label .
  }
  GRAPH WPP: {
    [] a wpp:Record ;
      ?field [
        wpp:source_concept ?iri ;
      ] ;
      wpp:record_source ?source .
    BIND(REPLACE(STR(?source), 'https://purl.wholepersonphysiome.org/wpp/', '') as ?table)
    FILTER(STRSTARTS(STR(?iri), STR(UBERON:)))
    BIND(REPLACE(STR(?iri), STR(UBERON:), 'UBERON:') as ?id)
  }
}
GROUP BY ?id ?label
ORDER BY ?id
